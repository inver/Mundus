import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
    ext.kotlinVersion = '1.8.0'
    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.9.0'
    }
}

allprojects {
    apply from: "$rootDir/gradle/dependencies.gradle"

    apply plugin: "jacoco"
    apply plugin: 'info.solidsoft.pitest'
    apply plugin: 'checkstyle'

    ext {
        appName = "Mundus"
        gdxVersion = '1.11.0'
        visuiVersion = '1.5.1'
        kryoVersion = '5.2.0'
        junitVersion = '4.13.2'
        mockitoVersion = '1.10.19'
        commonsIoVersion = '2.5'
        commonsLangVersion = '3.12.0'
        gltfVersion = 'master-SNAPSHOT'

        ktxVersion = '1.10.0-b2'
    }

    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url 'https://jitpack.io' }
    }

    tasks.withType(Checkstyle).configureEach {
        reports {
            xml.required = false
            html.required = true
            html.stylesheet resources.text.fromFile('../config/xsl/checkstyle-custom.xsl')
        }
    }

    tasks.withType(JavaCompile).configureEach {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    tasks.withType(KotlinCompile).configureEach {
        compilerOptions {
            jvmTarget.set(JvmTarget.JVM_11)
        }
    }

    tasks.withType(JacocoReport).configureEach {
        reports {
            xml.required = false
            csv.required = false
            html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
        }
    }

    tasks.withType(Test).configureEach {
        testLogging {
            exceptionFormat "full"
            showCauses true
            showExceptions true
            showStackTraces true
            showStandardStreams true
            events = ["passed", "skipped", "failed"]
        }
        finalizedBy(jacocoTestReport)
    }
}

project(":commons") {
    apply plugin: "java"

    dependencies {
        implementation project(":core")

        compileOnly 'org.projectlombok:lombok:1.18.24'
        annotationProcessor 'org.projectlombok:lombok:1.18.24'

        implementation 'org.lwjgl:lwjgl-assimp:3.3.1'
        implementation 'org.lwjgl:lwjgl-assimp:3.3.1:natives-linux'

        implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.4'
        implementation 'com.fasterxml.jackson.core:jackson-annotations:2.13.4'

        implementation "com.badlogicgames.gdx:gdx:$gdxVersion"
        implementation "com.github.mgsx-dev.gdx-gltf:gltf:$gltfVersion"
        implementation "net.onedaybeard.artemis:artemis-odb:2.3.0"
        implementation 'net.onedaybeard.artemis:artemis-odb-serializer-json:2.3.0'

        implementation group: 'commons-io', name: 'commons-io', version: '2.11.0'
        implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
        implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'
        implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.0'

        implementation 'org.locationtech.jts:jts-core:1.19.0'

        testCompileOnly 'org.projectlombok:lombok:1.18.24'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'
        testImplementation "junit:junit:$junitVersion"
        testImplementation 'org.mockito:mockito-core:4.8.0'
        testImplementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
        testImplementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        testImplementation 'ch.qos.logback:logback-classic:1.4.0'

    }


//
//    pitest {
//        targetClasses = ['com.mbrlabs.*']  //by default "${project.group}.*"
//        pitestVersion = '1.9.0' //not needed when a default PIT version should be used
//        threads = 4
//        outputFormats = ['HTML']
//        timestampedReports = false
//    }

//    build.dependsOn 'pitest'
}

project(":core") {
    apply plugin: "java"

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.24'
        annotationProcessor 'org.projectlombok:lombok:1.18.24'

        implementation 'org.lwjgl:lwjgl-assimp:3.3.1'
        implementation 'org.lwjgl:lwjgl-assimp:3.3.1:natives-linux'
        implementation 'org.lwjgl:lwjgl-assimp:3.3.1:natives-macos'
        implementation 'org.lwjgl:lwjgl-assimp:3.3.1:natives-macos-arm64'
        implementation 'org.lwjgl:lwjgl-assimp:3.3.1:natives-windows'

        implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.4'
        implementation 'com.fasterxml.jackson.core:jackson-annotations:2.13.4'

        implementation "com.badlogicgames.gdx:gdx:$gdxVersion"
        implementation "com.github.mgsx-dev.gdx-gltf:gltf:$gltfVersion"
        implementation "net.onedaybeard.artemis:artemis-odb:2.3.0"
        implementation 'net.onedaybeard.artemis:artemis-odb-serializer-json:2.3.0'

        implementation group: 'commons-io', name: 'commons-io', version: '2.11.0'
        implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
        implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'
        implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.0'

        implementation 'org.locationtech.jts:jts-core:1.19.0'

        testCompileOnly 'org.projectlombok:lombok:1.18.24'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'
        testImplementation "junit:junit:$junitVersion"
        testImplementation 'org.mockito:mockito-core:4.8.0'
        testImplementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
        testImplementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        testImplementation 'ch.qos.logback:logback-classic:1.4.0'

    }
}

project(":editor") {
    apply plugin: "java"
    apply plugin: "kotlin"

    dependencies {
        implementation project(":commons")
        implementation project(":core")

        compileOnly 'org.projectlombok:lombok:1.18.24'
        annotationProcessor 'org.projectlombok:lombok:1.18.24'

        testCompileOnly 'org.projectlombok:lombok:1.18.24'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'

        // Yep, we will use spring in desktop application =)
        implementation 'org.springframework:spring-context:5.3.23'
        implementation 'javax.annotation:javax.annotation-api:1.3.2'

        // Do normal json serialization
        implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.4'
        implementation 'com.fasterxml.jackson.core:jackson-annotations:2.13.4'

        // logging
        implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.0'
        implementation 'ch.qos.logback:logback-classic:1.4.0'
        implementation 'io.github.microutils:kotlin-logging-jvm:2.1.23'

        // Kotlin libs
        implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

        // libGDX
        implementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
        implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"

        // gdx-freetype
        implementation "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
        implementation "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
        implementation "com.github.mgsx-dev.gdx-gltf:gltf:$gltfVersion"
        implementation "net.onedaybeard.artemis:artemis-odb:2.3.0"

        // commons
        implementation "org.apache.commons:commons-lang3:$commonsLangVersion"
        implementation "commons-io:commons-io:$commonsIoVersion"

        // other
        implementation "com.kotcrab.vis:vis-ui:$visuiVersion"
        implementation "com.esotericsoftware:kryo:$kryoVersion"

        // tests
        testImplementation "junit:junit:$junitVersion"
        testImplementation 'org.mockito:mockito-core:4.8.0'
        testImplementation 'org.springframework:spring-test:5.3.23'
    }


    jacocoTestReport {
        dependsOn test // tests are required to run before generating the report
        reports {
            xml.required = false
            csv.required = false
            html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
        }
    }

    test {
        finalizedBy jacocoTestReport
    }
}